apply plugin: 'com.android.application'
apply from: "$project.rootDir/tools/script-git-version.gradle"
apply from: "$project.rootDir/tools/script-findbugs.gradle"
apply from: "$project.rootDir/tools/script-lint.gradle"
apply from: "$project.rootDir/tools/script-pmd.gradle"
apply from: "$project.rootDir/tools/script-java-code-coverage.gradle"
apply plugin: 'jacoco-android'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"
    defaultConfig {
        applicationId "com.rodrigobresan.weatherapplication"
        minSdkVersion 15
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release {
            storeFile file(System.getenv("KEYSTORE") ?: "$project.rootDir/keystore/weather_release.jks")
            storePassword System.getenv("KEYSTORE_PASSWORD")
            keyAlias System.getenv("KEY_ALIAS")
            keyPassword System.getenv("KEY_PASSWORD")
        }
    }

    productFlavors {
        free {
            versionCode gitVersionCodeTime
            versionName gitVersionName
            applicationId "com.rodrigobresan.weatherapplication"
        }

        pro {
            versionCode gitVersionCodeTime
            versionName gitVersionName
            applicationId "com.rodrigobresan.weatherapplication.pro"
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), "$project.rootDir/tools/rules-proguard.pro"
            signingConfig signingConfigs.release
        }

        debug {
            testCoverageEnabled true
            applicationIdSuffix ".dev"
            minifyEnabled false
            signingConfig signingConfigs.debug
        }
    }

    dataBinding {
        enabled = true
    }

    jacocoAndroidUnitTestReport {
        html.enabled true
    }

}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile 'com.android.support:appcompat-v7:25.1.0'
    testCompile "org.robolectric:robolectric:3.3.2"
    testCompile "org.assertj:assertj-core:2.5.0"
    testCompile 'junit:junit:4.12'
}